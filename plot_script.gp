#---------------------------------------------------------------------------
# This is a basic gnuplot script to generate a performance comparison
# graph. This example requires an "output.dat" file, which is
# generated by running:
#
#     bazel-bin/hw1 > output.dat
#
# To run this script type the following at the command line
#
#     gnuplot -c plot_script.gp
#
# After running the above two commands, the file hw1-perf.png
# will be generated. 
#---------------------------------------------------------------------------

infile = "output.dat"
outfile = "hw1-perf.png"

# Set the terminal for PNG output and aspect ratio
set terminal pngcairo enhanced font 'Verdana, 10'
set size ratio 0.75

# Save the graph in the given file name (e.g., "graph.png")
set output outfile

# Set the title and each axis label
set xlabel "Input Size (n)"
set ylabel "Time (micros)"

# Move the key to the left of the graph
set key right

# color scheme
RED = "#e6194B"
GREEN = "#3cb44b"
YELLOW = "#ffe119"
BLUE = "#4363d8"
ORANGE = "#f58231"
PURPLE = "#911eb4"
CYAN = "#42d4f4"
MAGENTA = "#f032e6"
LIME = "#bfef45"
PINK = "#fabed4"
TEAL = "#469990"
LAVENDER = "#dcbeff"
BROWN = "#9A6324"
BEIGE = "#fffac8"
MAROON = "#800000"
MINT = "#aaffc3"
OLIVE = "#808000"
APRICOT = "#ffd8b1"
NAVY = "#000075"
GREY = "#a9a9a9"


# Turn on the grid
set grid

set auto x
set xtics 0,2500,20000

set auto y
set yrange [0:6000]

# Plot the data
set title "AdjMatrix vs AdjList Performance Comparison";
plot  infile u 1:2 t "AdjMatrix Load Time" w linespoints lw 3 lc rgb MAGENTA pointtype 6, \
      infile u 1:3 t "AdjMatrix Avg Out Nodes" w linespoints lw 3 lc rgb ORANGE pointtype 6, \
      infile u 1:4 t "AdjMatrix Avg In Nodes" w linespoints lw 3 lc rgb PURPLE pointtype 6, \
      infile u 1:5 t "AdjList Load Time" w linespoints lw 3 lc rgb CYAN pointtype 6, \
      infile u 1:6 t "AdjList Avg Out Nodes" w linespoints lw 3 lc rgb GREEN pointtype 6, \
      infile u 1:7 t "AdjList Avg In Nodes" w linespoints lw 3 lc rgb NAVY pointtype 6;





